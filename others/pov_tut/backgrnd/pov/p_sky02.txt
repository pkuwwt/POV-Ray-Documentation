// POV-Ray version 3.6/3.7 scenery file "p_sky02.pov"
// author: Friedrich A. Lohmueller, 2000, update Dec-2009 / Jan-2011 / Nov-2013
// homepage: http://www.f-lohmueller.de

// sample for good clouds and bad horizon line

//-----------------------------------------------------------------------------
#version 3.7; // 3.6;
global_settings{ assumed_gamma 1.0 }
#default{ finish{ ambient 0.1 diffuse 0.9 }}
//-----------------------------------------------------------------------------

#include "colors.inc"
#include "textures.inc"
// camera ------------------------------------------------------------
#declare Camera_0 = camera{ angle 80 
                            right    x*image_width/image_height
                            location  <0.0 , 1.0 ,-3.0>
                            look_at   <0.0 , 2.0 , 0.0>}
camera{ Camera_0 }
// sun ---------------------------------------------------------------
light_source{ <1500,2500,-2500> color  rgb<1,1,1> }
// sky ---------------------------------------------------------------
sphere{ <0,0,0>,1 hollow  // 
        // layered textures:
        texture{ pigment{ color rgb<0.1,0.3,0.75>*0.6}
                 #if (version = 3.7 )  finish {emission 1 diffuse 0}
                 #else                 finish { ambient 1 diffuse 0}
                 #end 
               } // end texture 1

        texture{ pigment { bozo turbulence 0.65
                           octaves 6  omega 0.7 lambda 2 
                           color_map { 
                        [0.0  color rgb <0.95, 0.95, 0.95>*0.5 ]
                        [0.1  color rgb <1, 1, 1>*1.1 ]
                        [0.15 color rgb <0.85, 0.85, 0.85> ]
                        [0.5 color rgbt <1, 1, 1, 1>*1 ]
                        [1.0 color rgbt <1, 1, 1, 1>*1 ]
                            } // end color_map
                           rotate<10,23,0>
                           scale <0.3, 0.4, 0.2>*0.7
                         } // end pigment
                 #if (version = 3.7 )  finish {emission 1 diffuse 0}
                 #else                 finish { ambient 1 diffuse 0}
                 #end 
               } // end texture 2
                      
     scale 10000}
// ground ------------------------------------------------------------
plane { <0,1,0>, 0 
        texture{ pigment{color rgb<0.35,0.65,0.0>*0.8}
                 normal {bumps 0.75 scale 0.015}
               } // end of texture
      } // end of plane
//--------------------------------------------------------------------

//--------------------------------------------------------------------------
//---------------------------- objects in scene ----------------------------
//--------------------------------------------------------------------------
// a mirror sphere !!!!  
sphere{ <0,0,0>,0.4 scale <1,1,1> rotate<0,0,0> translate<0,0.7,0>
        texture{ Polished_Chrome }
      }
//---------------------------------------------------------------------------
