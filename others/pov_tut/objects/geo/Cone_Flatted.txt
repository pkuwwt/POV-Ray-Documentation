// POV-Ray 3.6/3.7 include file "Cone_Flatted.inc"
// author: Friedrich A, Lohmueller, July-2009
// homepage: http://www.f-lohmueller.de/
// email: Friedrich.Lohmueller_aT_t-online.de
//------------------------------------------------------------------------
#ifndef( Cone_Flatted_Inc_Temp)
#declare Cone_Flatted_Inc_Temp = version;
#version 3.6;

//---------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------
//----------------------------------------------------------------------------- /////////
#macro Cone_Flatted ( Radius_, // radius around x axis
                      Height_, // in x
                      Merge_On )
//-----------------------------------------------------
#if (Merge_On = 1 )
merge {
#else
union {
#end
 cone {<0,0,0>,1,<0,1,0>,0
       matrix < 1,0,0,
               -1,1,0,
                0,0,1,
                0,0,0  >
      }
 cone {<0,0,0>,1,<0,1,0>,0
       matrix < 1,0,0,
                1,1,0,
                0,0,1,
                0,0,0  >
      }
 intersection {
   plane {< 0, 1, 1>,0 translate y}
   plane {< 0, 1,-1>,0 translate y}
   plane {<-1,-1, 0>,0}
   plane {< 1,-1, 0>,0}
 }
scale<Radius_, Height_, Radius_>
rotate<0,0,-90>
} // -------------------------------------------------
#end //-------------------------------- end of macro
//----------------------------------------------------------------------------- /////////




//---------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------
// example:
/*
// ---------------------------------------------------------------------
#include "Cone_Flatted.inc"
// ---------------------------------------------------------------------
object{ Cone_Flatted ( 1, // Radius, around x axis
                       1, // Height, in x
                       0, // Merge_On,
                      ) // ------------------

         texture { pigment{ color rgb< 1.0, 1.0, 1.0> }
                // normal { bumps 0.5 scale 0.05 }
                   finish { phong 1 reflection 0.00}
                 } // end of texture
 translate< 0, 0, 0>
} // -------------------------------------------------------------------
//----------------------------------------------------------------------
*/


#version Cone_Flatted_Inc_Temp;
#end
//------------------------------------- end of include file