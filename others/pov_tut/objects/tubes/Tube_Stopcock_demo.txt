// POV-Ray 3.6 / 3.7 Scene File "Tube_Stopcock_000_demo.pov"
// author: Friedrich A. Lohmueller, June-2009/Jan-2011
// email: Friedrich.Lohmueller_at_t-online.de
// homepage: http://www.f-lohmueller.de
//--------------------------------------------------------------------------
#version 3.6; // 3.7;
global_settings{ assumed_gamma 1.0 }
#default{ finish{ ambient 0.05 diffuse 0.9 }}
//------------------------------------------------------------------------
#include "colors.inc"
#include "textures.inc"
#include "glass.inc"
#include "metals.inc"
#include "golds.inc"
#include "stones.inc"
#include "woods.inc"
#include "shapes.inc"
#include "shapes2.inc"
#include "functions.inc"
#include "math.inc"
#include "transforms.inc"
//------------------------------------------------------------------------
#declare Camera_0 = camera {/*ultra_wide_angle*/ angle 15      // front view
                            location  <0.0 , 1.0 ,-40.0>
                            right     x*image_width/image_height
                            look_at   <0.0 , 1.0 , 0.0>}
#declare Camera_1 = camera {/*ultra_wide_angle*/ angle  50   // diagonal view
                            location  < 3.50, 4.00,-4.00>
                            right     x*image_width/image_height
                            look_at   < 0.15, 0.750, 0.00> }
#declare Camera_2 = camera {/*ultra_wide_angle*/ angle 90  //right side view
                            location  <3.0 , 1.0 , 0.0>
                            right     x*image_width/image_height
                            look_at   <0.0 , 1.0 , 0.0>}
#declare Camera_3 = camera {/*ultra_wide_angle*/ angle 90        // top view
                            location  <0.0 , 3.0 ,-0.001>
                            right     x*image_width/image_height
                            look_at   <0.0 , 1.0 , 0.0>}
camera{Camera_1}
//------------------------------------------------------------------------
// sun -------------------------------------------------------------------
light_source{<1000,1500,-2500> color White*0.9}           // sun light
light_source{<3.50,4.00,-4.00> color rgb<0.9,0.9,1>*0.05} // flash light
// sky -------------------------------------------------------------------
sky_sphere{ pigment{ gradient <0,1,0>
                     color_map{ [0   color rgb<1,1,1>         ]//White
                                [0.4 color rgb<0.14,0.14,0.56>]//~Navy
                                [0.6 color rgb<0.14,0.14,0.56>]//~Navy
                                [1.0 color rgb<1,1,1>         ]//White
                              }
                     scale 2 }
           } // end of sky_sphere 
//------------------------------------------------------------------------

//------------------------------ the Axes --------------------------------
//------------------------------------------------------------------------
#macro Axis_( AxisLen, Dark_Texture,Light_Texture) 
 union{
    cylinder { <0,-AxisLen,0>,<0,AxisLen,0>,0.05
               texture{checker texture{Dark_Texture } 
                               texture{Light_Texture}
                       translate<0.1,0,0.1>}
             }
    cone{<0,AxisLen,0>,0.2,<0,AxisLen+0.7,0>,0
          texture{Dark_Texture}
         }
     } // end of union                   
#end // of macro "Axis()"
//------------------------------------------------------------------------
#macro AxisXYZ( AxisLenX, AxisLenY, AxisLenZ, Tex_Dark, Tex_Light)
//--------------------- drawing of 3 Axes --------------------------------
union{
#if (AxisLenX != 0)
 object { Axis_(AxisLenX, Tex_Dark, Tex_Light)   rotate< 0,0,-90>}// x-Axis
 text   { ttf "arial.ttf",  "x",  0.15,  0  texture{Tex_Dark} 
          scale 0.5 translate <AxisLenX+0.05,0.4,-0.10>}
#end // of #if 
#if (AxisLenY != 0)
 object { Axis_(AxisLenY, Tex_Dark, Tex_Light)   rotate< 0,0,  0>}// y-Axis
 text   { ttf "arial.ttf",  "y",  0.15,  0  texture{Tex_Dark}    
           scale 0.5 translate <-0.75,AxisLenY+0.50,-0.10>}
#end // of #if 
#if (AxisLenZ != 0)
 object { Axis_(AxisLenZ, Tex_Dark, Tex_Light)   rotate<90,0,  0>}// z-Axis
 text   { ttf "arial.ttf",  "z",  0.15,  0  texture{Tex_Dark}
               scale 0.5 translate <-0.75,0.2,AxisLenZ+0.10>}
#end // of #if 
} // end of union
#end// of macro "AxisXYZ( ... )"
//------------------------------------------------------------------------

#declare Texture_A_Dark  = texture {
                               pigment{ color rgb<1,0.45,0>}
                               finish { phong 1}
                             }
#declare Texture_A_Light = texture { 
                               pigment{ color rgb<1,1,1>}
                               finish { phong 1}
                             }

//object{ AxisXYZ( 3.5, 2.5, 7, Texture_A_Dark, Texture_A_Light) scale 0.5   no_shadow}
//-------------------------------------------------- end of coordinate axes


// ground -----------------------------------------------------------------
//---------------------------------<<< settings of squared plane dimensions
#declare RasterScale = 0.5;
#declare RasterHalfLine  = 0.025;
#declare RasterHalfLineZ = 0.025;
//-------------------------------------------------------------------------
#macro Raster(RScale, HLine)
       pigment{ gradient x scale RScale
                color_map{[0.000   color rgbt<1,1,1,0>*0.6]
                          [0+HLine color rgbt<1,1,1,0>*0.6]
                          [0+HLine color rgbt<1,1,1,1>]
                          [1-HLine color rgbt<1,1,1,1>]
                          [1-HLine color rgbt<1,1,1,0>*0.6]
                          [1.000   color rgbt<1,1,1,0>*0.6]} }
 #end// of Raster(RScale, HLine)-macro
//-------------------------------------------------------------------------


plane { <0,1,0>, 0    // plane with layered textures
        texture { Raster(RasterScale,RasterHalfLine ) rotate<0,0,0> }
        texture { Raster(RasterScale,RasterHalfLineZ) rotate<0,90,0>}
        no_shadow
      }
plane { <0,1,0>, 0    // plane with layered textures
        texture { pigment{color White*1.1}
                  finish {ambient 0.45 diffuse 0.85}}
        translate<0,-1,0>
      }
//------------------------------------------------ end of squared plane XZ

//--------------------------------------------------------------------------
//---------------------------- objects in scene ----------------------------
//--------------------------------------------------------------------------

//--------------------------------------------------------------------------
//--------------------------------------------------------------------------
//--------------------------------------------------------------------------
// ------------------------------------------- optional textures
#declare Tube_Texture =
          texture { pigment{ color rgb< 1, 1, 1>*0.75 }
                    normal { pigment_pattern{radial frequency 120 ramp_wave
                                         color_map {[0.0, rgb 0]
                                                    [0.5, rgb 0.05]
                                                    [1.0, rgb 0]}
                                         rotate<90,0,0> scale 0.5} 0.05}
                  //  normal { bumps 0.01 scale <0.005,0.15,0.15> }
                    finish { phong 1 reflection 0.00}
                 } // end of texture
#declare Tube_Inside_Texture =
          texture { pigment{ color rgb< 1, 1, 1>*0.55 }
                    normal { bumps 0.015 scale 0.005 }
                    finish { phong 1 reflection 0.00}
                 } // end of texture
#declare Flange_Texture_1 =
          texture { pigment{ color rgb< 1, 1, 1>*0.75 }
                    normal { bumps 0.015 scale 0.005 }
                    finish { phong 1 reflection 0.00}
                 } // end of texture
#declare Flange_Texture_2 =
          texture { pigment{ color rgb< 1, 1, 1>*0.65 }
                    normal { bumps 0.015 scale 0.005 }
                    finish { phong 1 reflection 0.00}
                 } // end of texture
#declare Flange_Hole_Texture =
          texture { pigment{ color rgb< 1, 1, 1>*0.55 }
                    normal { bumps 0.015 scale 0.005 }
                    finish { phong 1 reflection 0.00}
                 } // end of texture
#declare Nut_Texture_1 =
          texture { pigment{ color rgb< 1, 1, 1>*0.75 }
                    normal { bumps 0.015 scale 0.005 }
                    finish { phong 1 reflection 0.00}
                 } // end of texture
#declare Nut_Texture_2 =
          texture { pigment{ color rgb< 1, 1, 1>*0.55 }
                    normal { bumps 0.015 scale 0.005 }
                    finish { phong 1 reflection 0.00}
                 } // end of texture
#declare Test_Tube_Texture = 
           texture { pigment { color rgb<1,0.7,0>}
                   //normal  { bumps 0.5 scale <0.25, 0.005,0.005>}  
                     finish  {  phong 0.5 reflection 0.00 }
                   } // end of texture

//-------------------------------------------------------------------------------
#include "Tube_Round_000.inc"
//-------------------------------------------------------------------------------
#declare Tube_Rmaj1 = 1.20;
#declare Tube_R1   = 0.500;
#declare Tube_R2   = 0.400;
#declare T_D       = 0.015;
#declare Flange_R1 = 0.650;
#declare Flange_D = 0.050;
#declare Flange_Border_Radius = 0.01;
#declare Tube_L1 = 5;

#declare Tube_90 =
object{ Tube_Round_000 (
                   90, // Tube_Angle
                   Tube_Rmaj1, // T_Rmaj, // tube  major radius around z (lefthanded!)
                   T_D, // T_D, // tube material thickness
                   Tube_R1, // T_R, // radius
                   Flange_R1, // F_R, // flange radius
                   Flange_D, // F_D, // flange thickness
                   Flange_Border_Radius, // FB_R, // Flange_Border_Radius
                   1, // Nuts_ON  // 0 = no nuts but holes; 1 = nuts - no holes
                   0.065, // Nut_Scale
                   16, // Number_of_Nuts
                ) // ------------------------------------
      }//--------------------------------------------------------------------------
//--------------------------------------------------------------------------------
// ------------------------------------------- optional textures
#declare Tube_Texture =
          texture { pigment{ color rgb< 1, 1, 1>*0.75 }
                    normal { bumps 0.005 scale <0.005,0.15,0.15> }
                    finish { phong 1 reflection 0.00}
                 } // end of texture
//-------------------------------------------------------------------------------
#include "Tube_Linear_000.inc"
//-------------------------------------------------------------------------------
#declare Tube_Lin1=
object{ Tube_Linear_000 (
                   Tube_L1 , // T_L, // tube lenght in x
                   T_D, // T_D, // tube material thickness
                   Tube_R1, // T_R, // radius
                   Flange_R1, // F_R, // flange radius
                   Flange_D, // F_D, // flange thickness
                   Flange_Border_Radius, // FB_R, // Flange_Border_Radius
                   1, // Nuts_ON  // 0 = no nuts but holes; 1 = nuts - no holes
                   0.065, // Nut_Scale
                   16, // Number_of_Nuts
                ) // ------------------------------------
      }//--------------------------------------------------------------------------
//--------------------------------------------------------------------------------
#include "Tube_Stopcock_000.inc"
//--------------------------------------------------------------------------------
// all together
//--------------------------------------------------------------------------
object{ Tube_Stopcock_000( // total length in z: 4*R1 / in x+: 2*R1 
                           Tube_R1, // R1, // main tube radius:  1 ~ 0.25
                           Tube_R2, // R2, // side tube radius:  1.5 ~ 0.20
                           T_D, // Tube_D, // tube material thickness 
                           0.15, // Blobfactor; // 0.1~0.002 ; maximum = 0.20 !! 
                           0.05, //R_Correction, //   ~  Blobfactor/5
                           0, // Test_End_Cylinders_ON=1; off=0

                           Flange_R1-Tube_R1, // flange radius add
                           Flange_D, // F_D,  // flange thickness
                           Flange_Border_Radius, // FB_R, // Flange_Border_Radius
                           1, // Nuts_ON, // 0 = no nuts but holes; 1 = nuts - no holes
                           0.085, // Nut_Scale, // ~0.035
                           12, // Number_of_Nuts, // ~ 16,   
                          
                           1,// Side_End_Cover_ON,

                           1,// Stopcock_wheel_ON 
                           20,// Stopcock_wheel_Rotation_Angle 
                           // wheel sitzes
                           0.05, //  Wheel_Rmin, 
                           0.55, //  Wheel_Rmaj, 
                           0.35, //  Wheel_Axis_Len,   

                        ) // -----------------------------------------------
         rotate <0,0,90>  rotate <0,90,0>
      } // -----------------------------------------------------------------
//--------------------------------------------------------------------------
union{ 
  object{ Tube_90 rotate<90,90,0 > } 
  object{ Tube_Lin1 rotate<0,-90,0> translate<-Tube_Rmaj1, 0,0*Tube_L1> }
  object{ Tube_Lin1 rotate<0,-90,0> translate<-Tube_Rmaj1, 0,1*Tube_L1> }
  object{ Tube_Lin1 rotate<0,-90,0> translate<-Tube_Rmaj1, 0,2*Tube_L1> }

  translate< -4*Tube_R1,0,Tube_Rmaj1 >
}//

  object{ Tube_Lin1 rotate<0,0,0> translate<  4*Tube_R1 , 0,0*Tube_L1> }




//--------------------------------------------------------------------------




 